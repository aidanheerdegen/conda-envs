#%Module1.0

# Don't allow multiple conda environments to be loaded at once through modules
conflict conda

# Conda install directory
set condahome "/g/data/hh5/public/apps/miniconda3"

# Name of this module's environment
set condaenv [lindex [split [module-info name] {/}] 1]

if {[info exists ::env(CONDA_SHLVL)]} {
    set shlvl $::env(CONDA_SHLVL)
} else {
    set shlvl 0
}

if { [module-info shelltype] eq "csh" } {
    # Load manually, as the alias doesn't work within a module
    set-alias conda          "source source $condahome/etc/profile.d/conda.csh"
    setenv CONDA_DEFAULT_ENV $condaenv
    setenv CONDA_PREFIX      $condahome/envs/$condaenv;
    setenv CONDA_SHLVL       1
    prepend-path PATH        $condahome/envs/$condaenv/bin
} else {

    # Use the provided scripts
    if {[module-info mode load] || [module-info mode display]} {

        if {[module-info mode load]} {
            set stream stdout
        } else {
            set stream stderr
        }

        if {$shlvl > 0} {
            puts stderr "ERROR: A conda environment is already active"
            puts stderr "ERROR: Try 'conda deactivate'"
            break
        }

        puts $stream "source $condahome/etc/profile.d/conda.sh;"
        puts $stream "conda activate $condahome/envs/$condaenv;"
    }

    if {[module-info mode remove]} {
        if {$shlvl > 1} {
            puts stderr "ERROR: Multiple ($::env(CONDA_SHLVL)) conda environments have been loaded, cannot unload with module"
            puts stderr "ERROR: Try 'conda deactivate'"
            break
        }

        puts stdout "conda deactivate;"

        # Remove the 'conda' alias added by conda.sh
        unset-alias conda
    }

    # Needed for jupyter
    #append-path PATH        $condahome/bin
}

if [ file isdirectory "/short" ] {
    set userconda /short/$::env(PROJECT)/$::env(USER)/conda
} else {
    set userconda /local/$::env(PROJECT)/$::env(USER)/conda
}

setenv CONDA_ENVS_PATH $userconda/envs
setenv CONDA_PKGS_DIRS $userconda/pkgs

# Disable including libraries from ~/.local
# setenv PYTHONNOUSERSITE x
